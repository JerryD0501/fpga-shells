tuple FPGACompileOutputs =
  Job_ Job

global def getFPGACompileOutputsJob = getFPGACompileOutputsJob_

def getFPGACompileOptions board plan =
  def dut = plan.getSimCompileOptionsPlanDUT
  def hexFile =
    def sdboot = sources "{here}/bootloader" 'sdboot\.elf' | head
    makeProgramFromElf (makeRuntimeProgramArgs outputDir) sdboot
    | getProgramHex
  def rom =
    def romConf = filter (matches '.*\.rom\.conf' _.getPathName) dut.getDUTAllOutputs | head
    def outputFile = "{outputDir.getPathName}/rom_gen.v"
    makeVLSIRomGenOptions romConf (relative outputDir.getPathName hexFile.getPathName) outputFile
    | rocket_vlsi_rom_gen

  def outputDir = plan.getSimCompileOptionsPlanCompileDir
  def verilogFiles =
    def fpgaDir = "fpga-shells/xilinx"
    def powerOnReset = sources "{fpgaDir}/common/vsrc" 'PowerOnResetFPGAOnly\.v' | head
    def vcuVsrc = 	sources "{fpgaDir}/{board}/vsrc" 'vcu118reset\.v|sdio\.v'
    powerOnReset, rom, vcuVsrc ++ dut.getDUTVsrcs
  def topModule = dut.getDUTTopModule
  def allFiles = hexFile, dut.getDUTAllOutputs
  def tclFiles =
    allFiles
    | filter (matches '.*\.vivado\.tcl' _.getPathName)
  FPGACompileOptions topModule tclFiles outputDir verilogFiles allFiles board

def getFPGAExecuteOptions plan compileOutputs = compileOutputs

def fpgaSimulationChecker plan executeOutputs =
  def simStatus  = executeOutputs.getFPGACompileOutputsJob.getJobStatus
  def simStdout  = executeOutputs.getFPGACompileOutputsJob.getJobStdout
  def simStderr  = executeOutputs.getFPGACompileOutputsJob.getJobStderr
  def rawOutputs = executeOutputs.getFPGACompileOutputsJob.getJobFailedOutputs
  def executeDir = plan.getSimResultsPlanCompileDir
  makeSimOutputs simStatus simStdout simStderr rawOutputs executeDir

global def doFPGAExecute plan = plan

global def vc707Sim =
  makeSimulatorWithKey
  vc707Key
  (getFPGACompileOptions "vc707")
  doFPGACompile
  getFPGAExecuteOptions
  doFPGAExecute
  fpgaSimulationChecker

global def vcu118Sim =
  makeSimulatorWithKey
  vcu118Key
  (getFPGACompileOptions "vcu118")
  doFPGACompile
  getFPGAExecuteOptions
  doFPGAExecute
  fpgaSimulationChecker

tuple FPGACompileOptions =
  global TopModule  String
  global TclFiles   List Path
  global OutputDir  Path
  global Vsrcs      List Path
  global AllOutputs List Path
  global Board      String

global def doFPGACompile plan = memoize 0 (
  def board = plan.getFPGACompileOptionsBoard
  def buildDir = plan.getFPGACompileOptionsOutputDir.getPathName
  def verilogFiles = plan.getFPGACompileOptionsVsrcs
  def tclFiles = plan.getFPGACompileOptionsTclFiles
  def ffile =
    def contents =
      map (relative buildDir _.getPathName) verilogFiles
      | catWith "\n"
    write 0664 "{buildDir}/test.f" contents
  def sourceFile = sources "{here}/xilinx/common/tcl" 'vivado\.tcl' | head
  def common =
    sources "{here}/xilinx/common/tcl" '.*\.tcl'
    ++ sources "{here}/xilinx/{board}/tcl" '.*\.tcl'
  def cmdline =
    whichIn path "vivado",
    "-nojournal",
    "-mode", "batch",
    "-source", relative buildDir sourceFile.getPathName,
    "-tclargs",
    "-top-module", plan.getFPGACompileOptionsTopModule,
    "-F", relative buildDir ffile.getPathName,
    "-board", board,
    "-ip-vivado-tcls",
    map (relative buildDir _.getPathName) tclFiles | catWith " ",
    Nil

  def env = sifiveEnv ("xilinx/vivado", Nil) ("HOME=/tmp", environment)
  def path =
    env
    | filter (matches 'PATH=.*')
    | head
    | extract 'PATH=(.*)'
    | head
  def foutputs _ = files buildDir '.*'
  def inputs = ffile, verilogFiles ++ common ++ plan.getFPGACompileOptionsAllOutputs
  makeManualPlan cmdline inputs foutputs
  | setPlanEnvironment env
  | setPlanDirectory buildDir
  | runJob
  | FPGACompileOutputs
)

global def moduleEnv path list env =
  def cmd = which "modulecmd", "bash", "load", list
  def extra =
    makePlan cmd Nil
    | editPlanEnvironment ("MODULEPATH={path}", _)
    | runJob
    | getJobStdout
    | tokenize ";"
    | filter (matches '[^ ]*=.*')
    | map (replace ' *$' "")
  def pruned = filter (!matches "PATH=.*" _) env
  pruned ++ extra

global def sifiveEnv = moduleEnv "/etc/environment-modules/modules:/usr/share/modules/versions:/usr/Modules/3.2.10/modulefiles:/usr/share/modules/modulefiles:/sifive/tools/Modules/default/sifive"

global data FPGAVariant =
  VCU118
  VC707

global def fpgaVariantEquals = match _ _
  VCU118 VCU118 = True
  VC707  VC707  = True
  _      _      = False

global def vcu118Key = Key "vcu118" VCU118 fpgaVariantEquals
global def vc707Key  = Key "vc707" VC707 fpgaVariantEquals
global def vc707Target =
  def getResults sim plan =
    def compile = plan.getSimTargetResultsPlanCompile
    def execute = plan.getSimTargetResultsPlanExecute
    def results = plan.getSimTargetResultsPlanResults
    sim.getSimulatorResults compile execute results
  makeTarget vc707Key vc707Sim getResults

global def vcu118Target =
  def getResults sim plan =
    def compile = plan.getSimTargetResultsPlanCompile
    def execute = plan.getSimTargetResultsPlanExecute
    def results = plan.getSimTargetResultsPlanResults
    sim.getSimulatorResults compile execute results
  makeTarget vcu118Key vcu118Sim getResults

global def fpgaPlatform =
  makePlatform
  "vcu118-platform"
  (vc707Target, vcu118Target, Nil)

global def runFPGA = runDirSim fpgaPlatform _ allTests defaultBuildDir fpgaPlatform.getPlatformPlanDefaultTarget
global def runFPGABoard = runDirSim fpgaPlatform _ allTests defaultBuildDir _
